%{
open System
open TestDslAst
open CommonDslAst
open TextUtilities
open Common
open CompilationUnit
open System.Collections.Generic
open Errors
open Positioning
open DataStructures
open Navigation

let mutable errorLogger = Unchecked.defaultof<ErrorLogger>

let reportRangeI posRange msg = reportRange posRange msg errorLogger

let reportPointI pos msg = reportPoint pos msg errorLogger

let testIndex   = PositionSet<IndexTerm>()

let addTestIdx posRange term = testIndex.Insert(PositionIndex(posRange, term))

let separateRegistrations registrations = 
    let rec separateRec classRegs moduleRegs = function
        | RegistrationTerm.Class(a, b, c, d, e)::t  -> 
            let classReg = 
                {
                    Dependency = a;
                    Implementation = b;
                    DependencyPosition = c;
                    ArrowTermPosition = d;
                    ImplementationPosition = e;
                } 
            (classReg::classRegs, moduleRegs, t) |||> separateRec
        | RegistrationTerm.Module(a, b, c)::t -> 
            let moduleReg = 
                {
                    Name = a;
                    ModuleTermPosition = b;
                    NamePosition = c;
                }
            (classRegs, moduleReg::moduleRegs, t) |||> separateRec
        | _::t -> (classRegs, moduleRegs, t) |||> separateRec
        | [] -> (classRegs, moduleRegs)

    separateRec [] [] registrations

let separate lexems = 
    let rec separateRec (boolFlags1: BoolFlag1 list) (boolFlags2: BoolFlag2 list) (classRegistrations:  ClassRegistration list) (moduleRegistrations:  ModuleRegistration list) = function
        | (Declaration.BoolFlag1(BoolFlag1Term.Flag(a, b, c, d, e)))::t -> 
            let bFlag = { BoolFlag1.Value = a; BoolFlagPosition = b; EqualTermPosition = c; ValuePosition = d; WholePosition = e; }
            (bFlag::boolFlags1, boolFlags2, classRegistrations, moduleRegistrations, t)  |||||> separateRec 
        | (Declaration.BoolFlag2(BoolFlag2Term.Flag(a, b, c, d, e)))::t ->
            let bFlag = { BoolFlag2.Value = a; BoolFlagPosition = b; EqualTermPosition = c; ValuePosition = d; WholePosition = e; }
            (boolFlags1, bFlag::boolFlags2, classRegistrations, moduleRegistrations, t)  |||||> separateRec
        | Declaration.Registration(r)::t -> 
            let classRegs, moduleRegs = separateRegistrations r
            (boolFlags1, boolFlags2, List.append classRegs classRegistrations, List.append moduleRegs moduleRegistrations, t) |||||> separateRec
        | any::t -> (boolFlags1, boolFlags2, classRegistrations, moduleRegistrations, t) |||||> separateRec
        | []     -> (boolFlags1 |> List.rev, boolFlags2 |> List.rev, classRegistrations, moduleRegistrations)

    separateRec [] [] [] [] lexems

let useValidUsings usings =
    let rec useValid usings acc = 
        match usings with 
            | Using.Fqn(s, _)::t -> (t, s::acc) ||> useValid
            | Using.Iqn(_)::t 
            | Using.Orphan(_)::t -> (t, acc) ||> useValid
            | [] -> acc
    useValid usings []
%}

%token TESTHEADER
%token ARROW
%token EQ
%token BF1
%token BF2
%token TRUE
%token FALSE
%token QUOT
%token MODULE
%token <string> FQN
%token <string> IQN
%token <string> SNAME
%token USING
%token EOF

%start parseDrt
%type <TestCompilationUnit> parseDrt

%%

parseDrt:
    | testCompilationUnit EOF { $1 }
    | EOF { {TestCompilationUnit.Usings = []; Declarations = []; Errors = [] } }

testHeader:
    | TESTHEADER SNAME { TestHeader.Full($2, posRange parseState 1, posRange parseState 2) }
    | TESTHEADER       
        { 
            (endPos parseState 1, ErrMsg.TestNameIsNotDefined) ||> reportPointI
            TestHeader.Partial(posRange parseState 1)
        }
    | any 
        { 
            let errPos, errToken = $1
            let term = HeaderError(errPos, errToken)
            (errPos, ErrMsg.TestHeaderExpected) ||> reportRangeI
            //(errPos, TestHeaderTerm term)  ||> addTestIdx
            
            term
        }

testCompilationUnit:
    | usings tests {
                     {TestCompilationUnit.Usings = useValidUsings $1; Declarations = $2; Errors = errorLogger.Diagnostics; }
                   }
    | tests        {
                     {TestCompilationUnit.Usings = []; Declarations = $1; Errors = errorLogger.Diagnostics; } 
                   }

tests:
    | testBody tests { $1::$2 }
    | testBody       { [$1] }
    

   
testBody:
    | testHeader expressionSet
        {
            (posRange parseState 1, $1 |> TestHeaderTerm) ||> addTestIdx
            let boolFlags1, boolFlags2, classRegistrations, moduleRegistrations = separate $2
            Test($1, boolFlags1, boolFlags2, classRegistrations, moduleRegistrations, posRangeOut parseState 1 2)
        }
    | testHeader
        { 
            (posRange parseState 1, $1 |> TestHeaderTerm) ||> addTestIdx
            Test($1, [], [], [], [], posRange parseState 1)
        }
        
usings:
    | using usings { $1::$2 }
    | using        { [ $1 ] }

using:
    | USING SNAME { 
                    let term = Using.Fqn($2, posRangeOut parseState 1 2)
                    (posRangeOut parseState 1 2, UsingTerm term) ||> addTestIdx
                    term
                  }
    | USING IQN   {
                    let term = Using.Iqn($2, posRangeOut parseState 1 2)
                    (posRangeOut parseState 1 2, UsingTerm term) ||> addTestIdx
                    term
                  }
    | USING FQN   {
                    let term = Using.Fqn($2, posRangeOut parseState 1 2)
                    (posRangeOut parseState 1 2, UsingTerm term) ||> addTestIdx
                    (posRange parseState 2, ErrMsg.IncompleteName $2) ||> reportRangeI
                    term
                  }
    | USING       {
                    let term = Using.Orphan(posRange parseState 1)
                    (posRange parseState 1, UsingTerm term) ||> addTestIdx
                    (endPos parseState 1, ErrMsg.MissingNamespace) ||> reportPointI
                    term
                  }

expressionSet:
    | expressionSet bodyExpression  { $2::$1 |> List.rev }
    | bodyExpression  { [$1] }

bodyExpression:
    | boolFlag1       
                { 
                  (posRange parseState 1, $1 |> IndexTerm.BoolFlag1Term) ||> addTestIdx
                  BoolFlag1($1)
                }
    | boolFlag2       
                { 
                  (posRange parseState 1, $1 |> IndexTerm.BoolFlag2Term) ||> addTestIdx
                  BoolFlag2($1)
                }
    | registrationSet { Declaration.Registration($1 |> List.rev) }
    | errorExprBody   { 
                        let pos  = posRange parseState 1
                        let term = pos |> ErrorTerm.Error
                        (pos, (term, $1) |> IndexTerm.Error) ||> addTestIdx
                        term |> Declaration.Error
                      }

registrationSet:
    | registration {
                     (posRange parseState 1, $1 |> RegistrationTerm) ||> addTestIdx
                     [$1]
                   }
    | registrationSet registration 
                   { 
                     (posRange parseState 2, $2 |> RegistrationTerm) ||> addTestIdx
                     $2::$1 
                   }

registration:
    | name ARROW name   { RegistrationTerm.Class($1, $3, posRange parseState 1, posRange parseState 2, posRange parseState 3) }
    | MODULE SNAME      { RegistrationTerm.Module($2, posRange parseState 1, posRange parseState 2) }
	| errorRegistration { $1 }

name:
    | SNAME { $1 }
    | FQN   { $1 } 
    | IQN   { 
              let lexbuf = parseState.ParserLocalStore.["LexBuffer"] :?> LexBuffer<char>
              (posRange parseState 1, ErrMsg.IncompleteName (new string(lexbuf.Lexeme))) ||> reportRangeI
              $1
            }

boolFlag1:
    | BF1 EQ boolValue { BoolFlag1Term.Flag($3,  posRange parseState 1, startPos parseState 2, posRange parseState 3, posRangeOut parseState 1 3) }
    | errorBoolFlag1   { $1 }

boolFlag2:
    | BF2 EQ boolValue { BoolFlag2Term.Flag($3,  posRange parseState 1, startPos parseState 2, posRange parseState 3, posRangeOut parseState 1 3) }
    | errorBoolFlag2   { $1 }

boolValue:
    | TRUE  { true  }
    | FALSE { false }

errorBoolFlag1:
    | BF1 EQ        { 
                        (endPos parseState 2, ErrMsg.BoolMissing) ||> reportPointI
                        BoolFlag1Term.Error(BoolFlagMissingPart.Value(endPos parseState 2, startPos parseState 1))
                    }
    | BF1 EQ SNAME  { 
                        let lexbuf   = parseState.ParserLocalStore.["LexBuffer"] :?> LexBuffer<char>
                        let errToken = new string(lexbuf.Lexeme)
                        let errPos   = posRange parseState 3
                        (errPos, ErrMsg.UnexpectedToken errToken) ||> reportRangeI
                        BoolFlag1Term.Error(BoolFlagMissingPart.IncompleteValue(errPos, errToken))
                    }
    | BF1 boolValue { 
                       (endPos parseState 1, ErrMsg.EqMissing) ||> reportPointI
                       BoolFlag1Term.Error(BoolFlagMissingPart.EqualBetween(posRangeIn parseState 1 2))
                    }
    | BF1           { 
                        (endPos parseState 1, ErrMsg.EqMissing) ||> reportPointI
                        BoolFlag1Term.Error(BoolFlagMissingPart.EqualAfter(endPos parseState 1))
                    }
    | EQ boolValue  {
                       (startPos parseState 1, ErrMsg.BoolFlagTokenExpected) ||> reportPointI
                       BoolFlag1Term.Error(BoolFlagMissingPart.BoolFlagTerm(startPos parseState 1)) 
                    }
    | boolValue     {
                       (startPos parseState 1, ErrMsg.BoolFlagTokenExpected) ||> reportPointI
                       BoolFlag1Term.Error(BoolFlagMissingPart.BoolFlagTerm(startPos parseState 1))
                    }

errorBoolFlag2:
    | BF2 EQ        { 
                        (endPos parseState 2, ErrMsg.BoolMissing) ||> reportPointI
                        BoolFlag2Term.Error(BoolFlagMissingPart.Value(endPos parseState 2, startPos parseState 1))
                    }
    | BF2 EQ SNAME  { 
                        let lexbuf   = parseState.ParserLocalStore.["LexBuffer"] :?> LexBuffer<char>
                        let errToken = new string(lexbuf.Lexeme)
                        let errPos   = posRange parseState 3
                        (errPos, ErrMsg.UnexpectedToken errToken) ||> reportRangeI
                        BoolFlag2Term.Error(BoolFlagMissingPart.IncompleteValue(errPos, errToken))
                    }
	| BF2 boolValue {      
                        (endPos parseState 1, ErrMsg.EqMissing) ||> reportPointI
                        BoolFlag2Term.Error(BoolFlagMissingPart.EqualBetween(posRangeIn parseState 1 2))
                    }
    | BF2           { 
                        (endPos parseState 1, ErrMsg.EqMissing) ||> reportPointI
                        BoolFlag2Term.Error(BoolFlagMissingPart.EqualAfter(endPos parseState 1))
                    }

errorRegistration:
    | name       {
                   (endPos parseState 1, ErrMsg.ArrowMissing) ||> reportPointI
                   RegistrationTerm.ClassError(ClassRegMissingPart.ArrowAfter(endPos parseState 1)) 
                 }
	| name name  { 
                   (endPos parseState 1, ErrMsg.ArrowMissing) ||> reportPointI
                   RegistrationTerm.ClassError(ClassRegMissingPart.ArrowBetween(posRangeIn parseState 1 2)) 
                 }
	| name ARROW { 
                   (endPos parseState 2, ErrMsg.FqnMissing) ||> reportPointI
                   RegistrationTerm.ClassError(ClassRegMissingPart.ImplName(endPos parseState 2))
                 }
    | ARROW name { 
                   (endPos parseState 1, ErrMsg.FqnMissing) ||> reportPointI
                   RegistrationTerm.ClassError(ClassRegMissingPart.DepName(posRangeOut parseState 1 2))
                 }
    | ARROW      {
                   (startPos parseState 1, ErrMsg.OrphanArrow) ||> reportPointI
                   RegistrationTerm.ClassError(ClassRegMissingPart.OrphanArrow(posRange parseState 1))
                 }  
	| MODULE     { 
                   (endPos parseState 1, ErrMsg.FqnMissing) ||> reportPointI
                   RegistrationTerm.ModuleError(ModuleRegMissingPart.Name(endPos parseState 1))
                 }

errorExprBody:
    | errorExprBody recover { $2::$1 }
    | recover               { [ $1 ] }

errToken:
    | SNAME { 
                let lexbuf   = parseState.ParserLocalStore.["LexBuffer"] :?> LexBuffer<char>
                let posRange = (lexbuf.StartPos, lexbuf.EndPos)
                let errToken = new string(lexbuf.Lexeme)
                (posRange, ErrMsg.UnexpectedToken errToken) ||>reportRangeI
                (posRange, errToken)
            }    

recover:
    | error
        {
            let lexbuf   = parseState.ParserLocalStore.["LexBuffer"] :?> LexBuffer<char>
            let posRange = (lexbuf.StartPos, lexbuf.EndPos)
            let errToken = new string(lexbuf.Lexeme)
            (posRange, ErrMsg.UnexpectedToken errToken) ||>reportRangeI
            (posRange, errToken)
        }

any:
    | TESTHEADER   { posRangeAndToken parseState }
    | ARROW        { posRangeAndToken parseState }
    | EQ           { posRangeAndToken parseState }
    | BF1          { posRangeAndToken parseState }
    | BF2          { posRangeAndToken parseState }
    | TRUE         { posRangeAndToken parseState }
    | FALSE        { posRangeAndToken parseState }
    | QUOT         { posRangeAndToken parseState }
    | MODULE       { posRangeAndToken parseState }
    | FQN          { posRangeAndToken parseState }
    | IQN          { posRangeAndToken parseState }
    | SNAME        { posRangeAndToken parseState }

